#lang racket/base

(require racket/format
         html-writing
         "../private/common.rkt"
         "common.rkt")

(define (main outp)
  (define config (read (open-input-string (get-from-site "config.rktd"))))
  (define tasks (hash-ref config 'tasks null))
  (define task-map
    (for/hash ([task (in-list tasks)])
      (values (hash-ref task 'name) task)))

  (define (group-header title)
    `(tr (td (@ [class "group-header"] [colspan "2"]) ,title)))

  (define (row #:title title #:id id)
    (define link (~a id "-beat.json"))
    `(tr (@ [class "table-data"]
            [data-period ,(~a (task-period (hash-ref task-map id) config))]
            [data-link ,link])
         (td ,title)
         (td (a (@ [href ,link]) "Link"))))

  (displayln "<!-- This file is generated by gen-index.rkt. Do not manually edit this file. -->" outp)
  (displayln "<!DOCTYPE html>" outp)
  (write-html
   `((html
      (@ [lang "en"])
      (head (title "Racket Services: Most Recent Heartbeat")
            (meta (@ [charset "utf-8"]))
            (meta (@ [name "viewport"]
                     [content "width=device-width, initial-scale=1"]))
            (link (@ [rel "stylesheet"]
                     [href "./style.css"])))
      (body
       (main
        (h1 "Racket Services: Most Recent Heartbeat Dashboard")
        (table
         (thead
          (tr (@ [id "table-head"])
              (th (@ [scope "col"]) "Service")
              (th (@ [scope "col"]) "Raw link")))
         (tbody
          ,(group-header '(a (@ [href "https://pkgs.racket-lang.org/"])
                             "Package server"))
          ,(row #:title "Update" #:id "pkgd-update")
          ,(row #:title "Upload" #:id "pkgd-upload")

          ,(group-header '(a (@ [href "https://pkg-build.racket-lang.org/"])
                             "pkg-build"))
          ,(row #:title "pkg-build" #:id "pkg-build")

          ,(group-header '(a (@ [href "http://drdr.racket-lang.org/"]) "DrDr"))
          ,(row #:title "Poll" #:id "drdr-poll")
          ,(row #:title "Run" #:id "drdr-run")
          ,(row #:title "Disk" #:id "drdr-disk")

          ,(group-header '(a (@ [href "https://snapshot.racket-lang.org/"])
                             "Snapshots"))
          ,(row #:title "Utah" #:id "snapshot-utah")
          ,(row #:title "Northwestern" #:id "nwu-pkg-build-and-snapshot")

          ,(group-header '(a (@ [href "https://build-plot.racket-lang.org/"])
                             "Build plot"))
          ,(row #:title "CS" #:id "build-plot-cs")
          ,(row #:title "BC" #:id "build-plot-bc")

          ,(group-header "Condensed")
          ,(row #:title "Logs" #:id "condense-logs")
          ,(row #:title "West logs" #:id "condense-west-logs")

          ,(group-header "Take pulse")
          ,(row #:title "Utah" #:id "utah-monitor")
          ,(row #:title "Northwestern" #:id "nwu-monitor"))))

       (script (@ [src "script.js"])))))

   outp))

(module+ main
  (call-with-output-file* "index.html"
    #:exists 'replace
    main))
