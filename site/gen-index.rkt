#lang racket

(require html-writing)

(define (group-header title)
  `(tr (td (@ [class "group-header"]
              [colspan "2"])
           ,title)))

(define (row #:title title
             #:link link
             #:expected-freq expected-freq)
  `(tr (@ [class "table-data"]
          [data-freq ,(~a expected-freq)]
          [data-link ,link])
       (td ,title)
       (td (a (@ [href ,link]) "Link"))))

(define 25hrs (* 60 60 25))

(define (main)
  (displayln "<!-- This file is generated by gen-index.rkt. Do not manually edit this file. -->")
  (displayln "<!DOCTYPE html>")
  (write-html
   `((html
      (@ [lang "en"])
      (head (title "Racket Services: Most Recent Heartbeat")
            (meta (@ [charset "utf-8"]))
            (meta (@ [name "viewport"]
                     [content "width=device-width, initial-scale=1"]))
            (link (@ [rel "stylesheet"]
                     [href "./style.css"])))
      (body
       (main
        (h1 "Racket Services: Most Recent Heartbeat Dashboard")
        (table
         (thead
          (tr (@ [id "table-head"])
              (th (@ [scope "col"]) "Service")
              (th (@ [scope "col"]) "Raw link")))
         (tbody
          ,(group-header '(a (@ [href "https://pkgs.racket-lang.org/"])
                             "Package server"))
          ,(row #:title "Update"
                #:link "pkgd-update-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "Upload"
                #:link "pkgd-upload-beat.json"
                #:expected-freq 25hrs)

          ,(group-header '(a (@ [href "https://pkg-build.racket-lang.org/"])
                             "pkg-build"))
          ,(row #:title "pkg-build"
                #:link "pkg-build-beat.json"
                #:expected-freq 25hrs)

          ,(group-header '(a (@ [href "http://drdr.racket-lang.org/"]) "DrDr"))
          ,(row #:title "Poll"
                #:link "drdr-poll-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "Run"
                #:link "drdr-run-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "Disk"
                #:link "drdr-disk-beat.json"
                #:expected-freq 25hrs)

          ,(group-header '(a (@ [href "https://snapshot.racket-lang.org/"])
                             "Snapshots"))
          ,(row #:title "Utah"
                #:link "snapshot-utah-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "Northwestern"
                #:link "nwu-pkg-build-and-snapshot-beat.json"
                #:expected-freq 25hrs)

          ,(group-header '(a (@ [href "https://build-plot.racket-lang.org/"])
                             "Build plot"))
          ,(row #:title "CS"
                #:link "build-plot-cs-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "BC"
                #:link "build-plot-bc-beat.json"
                #:expected-freq 25hrs)

          ,(group-header "Condensed")
          ,(row #:title "Logs"
                #:link "condense-logs-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "West logs"
                #:link "condense-west-logs-beat.json"
                #:expected-freq 25hrs)

          ,(group-header "Take pulse")
          ,(row #:title "Utah"
                #:link "utah-monitor-beat.json"
                #:expected-freq 25hrs)
          ,(row #:title "Northwestern"
                #:link "nwu-monitor-beat.json"
                #:expected-freq 25hrs))))

       (script (@ [src "script.js"])))))

   (current-output-port)))

(module+ main
  (with-output-to-file "index.html"
    #:exists 'replace
    main))
